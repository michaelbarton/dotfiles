#!/bin/zsh

autoload -U is-at-least add-zsh-hook

autoload -U edit-command-line
zle -N edit-command-line

# Autoload zsh modules when they are referenced
zmodload -a zsh/stat stat
zmodload -a zsh/zpty zpty
zmodload -a zsh/zprof zprof
zmodload -ap zsh/mapfile mapfile

unsetopt BG_NICE		# do NOT nice bg commands

setopt ALL_EXPORT

# Case insensitive globbing
setopt extendedglob
unsetopt CASE_GLOB

setopt CORRECT	            # command CORRECTION
setopt NO_MENU_COMPLETE     # no menu completion of commands
setopt LIST_AMBIGUOUS       # unambiguous prefix completion is done silently
setopt NO_LIST_BEEP         # don't beep for completion
setopt LIST_PACKED          # different width for completion
setopt NO_LIST_ROWS_FIRST   # sort by colums
setopt LIST_TYPES

# Set/unset  shell options
setopt   notify globdots correct pushdtohome cdablevars autolist
setopt   correctall autocd recexact longlistjobs
setopt   autoresume histignoredups pushdsilent noclobber
setopt   autopushd pushdminus extendedglob rcquotes mailwarning
unsetopt bgnice autoparamslash
#!/bin/zsh

if [ x"$HOME" = x ] ; then
        export HOME=$(cd ~ ; pwd)
fi

if [ x"$HOSTNAME" = x ] ; then
        export HOSTNAME=$(hostname)
fi

if [ -x "$HOME/.local" ] ; then
  source "$HOME/.local"
fi

fpath=($fpath $HOME/.zsh-git/functions)

# Use ~/cache as default working directory
cdpath=( ~/cache )
cd ~/cache

# !/bin/zsh

setopt SHARE_HISTORY        # Share history between multiple sessions
setopt EXTENDED_HISTORY     # puts timestamps in the history
setopt HIST_ALLOW_CLOBBER
setopt HIST_REDUCE_BLANKS
setopt INC_APPEND_HISTORY
setopt HIST_IGNORE_ALL_DUPS # ignore all -- even non contiguous
setopt HIST_IGNORE_SPACE    # Ignore history commands beginning with space
setopt HIST_NO_STORE        # Don't store the history command in history

HISTFILE=$HOME/.history
HISTSIZE=1000
SAVEHIST=1000
#!/bin/zsh

autoload zmv
alias mmv='noglob zmv -W'

# Global aliases
alias -g X='| xargs'
alias -g XX='| xargs -I {}'
alias -g X0='| xargs -0'
alias -g XX0='| xargs -0 -I {}'

# Suffix based aliases
alias -s gz=gunzip
alias -s bz2=bunzip2
alias -s zip=unzip
alias -s tar.gz='tar xzf'
alias -s tgz='tar xzf'
alias -s tar.bz2='tar xjf'

alias -s rb='ruby -rubygems'
alias -s py=python

# Avoid spelling corrections on certain commands
alias mv='nocorrect mv'
alias cp='nocorrect cp'
alias mkdir='nocorrect mkdir'
#!/bin/zsh

# Search through history
bindkey -v "^R" history-incremental-search-backward

# History completion
bindkey -v "^[[A" history-beginning-search-backward
bindkey -v "^[[B" history-beginning-search-forward

# Editor current line in editor
bindkey -M vicmd v edit-command-line
#!/bin/zsh

# Reduce time it take for tab completion
__git_files () {
  _wanted files expl 'local files' _files
}

zstyle ':completion:*' add-space true
zstyle ':completion:*' completer _expand _complete _match _prefix _approximate _list
zstyle ':completion:*' menu select=1
zstyle ':completion:*' file-sort name
zstyle ':completion:*' list-colors ${(s.:.)ZLS_COLORS}
zstyle ':completion:*' matcher-list 'r:|[._-]=** r:|=**' 'l:|=** r:|=**'
zstyle ':completion:*' menu select
zstyle ':completion:*:approximate:*' max-errors 'reply=( $(( ($#PREFIX+$#SUFFIX)/3 )) numeric )'

#[ Formats ]####################################################################
zstyle ':completion:*' group 1
zstyle ':completion:*' format '%B---- %d%b'
zstyle ':completion:*:corrections' format '%B---- %d (errors %e)%b'
zstyle ':completion:*:descriptions' format "%B---- %d%b"
zstyle ':completion:*:messages' format '%B%U---- %d%u%b' 
zstyle ':completion:*:warnings' format "%B$fg[red]%}---- no match for: $fg[white]%d%b"
zstyle ':completion:*' group-name ''

#[ Kill ]#######################################################################
zstyle ':completion:*:processes' command 'ps -au$USER -o pid,time,cmd|grep -v "ps -au$USER -o pid,time,cmd"'
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)[ 0-9:]#([^ ]#)*=01;30=01;31=01;38'

#[ hosts and users ]############################################################
#hosts=()
#[ -r ~/.ssh/config ] && hosts=(${${${$(grep '^Host' ~/.ssh/config)}##Host }##[*0-9]*})
#[ -r ~/.ssh/known_hosts ] && hosts=(${${${${(f)"$(<$HOME/.ssh/known_hosts)"}:#[0-9]*}%\*}%,*})
hosts=($( ( \
        ( [ -r ~/.ssh/config ] && awk '/^host +[a-z]/ { print $2 }' ~/.ssh/config) ; \
        ( [ -r ~/.ssh/known_hosts ] && awk '{print $1}' ~/.ssh/known_hosts | tr , '\n') \
) | sort -u))

zstyle ':completion:*' hosts $hosts
zstyle ':completion:*:hosts' list-colors '=(#b)(*)(.jukie.net)=01;30=01;31' '=[^.]#=01;31'

users=(root)
zstyle ':completion:*' users $users

zstyle ':completion:*:*:[ak]dvi:*' file-patterns \
    '*.dvi:dvi-files:DVI\ files *(-/):directories:Directories' '*:all-files'
zstyle ':completion:*:*:kghostview:*' file-patterns \
    '*.(ps|pdf)(|.gz|.bz2):pspdf-files:PostScript\ or\ PDF\ files  *(-/):directories:Directories' '*:all-files'
zstyle ':completion:*:*:swfplayer:*' file-patterns \
    '*.swf:swf-files:Swf\ files  *(-/):directories:Directories' '*:all-files'

zstyle ':completion:*' file-patterns \
    '%p:globbed-files: *(-/):directories:Directories' '*:all-files'

#[ ignores for vim ]############################################################

zstyle ':completion:*:*:vi(m|):*:*files' ignored-patterns '*?.(aux|dvi|ps|pdf|bbl|toc|lot|lof|o|cm?)'
# Jeweler gem creation options
export JEWELER_OPTS="--rspec --yard --cucumber --user-email $EMAIL"

# I may forget this ...
alias autospec_setup='rspec --configure autotest'

# Run cucumber tests with autospec
alias autocuc='AUTOFEATURE=true autotest'

# Autospec is depreciated in latest version of rspec
alias autospec='autotest'
#!/bin/zsh

setopt prompt_subst
autoload colors
colors

autoload -Uz promptinit; promptinit
autoload -U zgitinit; zgitinit

reset="%{%b%s%u$reset_color%}"
current_directory="%{$fg_bold[cyan]%c%}"
end="%{$fg_bold[red]%} : "

function status() { "$@"; echo $? }


# See https://github.com/jcorbin/zsh-git/issues/7
function git_prompt() {
  zgit_isgit || return
  local msg

  if zgit_inworktree; then
    local fmt
    zstyle -s ':promptinfo:' git-status fmt
    [[ -z ${fmt} ]] && fmt='%(i.-.+)%(w.-.*)%(m.!.-)%(t.?.-)'

    zformat -f msg ${fmt} \
       i:$(status zgit_isindexclean) \
       w:$(status zgit_isworktreeclean) \
       m:$(status zgit_hasunmerged) \
       t:$(status zgit_hasuntracked)
  fi

  echo -n " %{$fg_bold[red]%}($reset%{$fg_bold[blue]%}$(zgit_branch)$reset:${msg}%{$fg_bold[red]%})$reset"
}

function git_branch() {
}

PROMPT='\
$reset\
$current_directory\
$reset\
$(git_prompt)\
$reset\
$end\
$reset\
'
#!/bin/zsh

# this removes duplicates from PATH, CDPATH, MANPATH, and FPATH
typeset -U path cdpath manpath fpath

